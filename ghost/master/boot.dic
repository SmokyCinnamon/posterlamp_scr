OnFirstBoot
{
	//If you add variables, make sure to initialize them here!
	LastTalk = ""
	rainType = "Off"
	sessionFace = ANY(facesPool)
	sessionText = ANY(textPool)
	
	"\1\s[10]\0\s[0](It's the community lamppost.)\x[noclear]\n\n(It's full of 'missing' posters.)"
}

OnInitialize
{
	sessionFace = ANY(facesPool)
	sessionText = ANY(textPool)
	//If you want some variables to reset on boot, put them here. This event will happen even if you add other events like OnGhostChanged
	//OnInitialize is a NOTIFY event! That means it cannot return scripts. This event is only meant for things like variable changes. See this page on Ukadoc for more info: https://ukagakadreamteam.github.io/ukadoc/manual/spec_shiori3.html
	
	//If you add new variables in updates, I recommend initializing them like so:
	//if !ISVAR("NewVariable"); NewVariable = 0
	//This means that it will only set the new variable if the user doesn't already have it, so that their variables don't get overwritten
	
	if reference0 != "reload" //For things you want to reset EXCEPT when just reloading; a convenience for you the dev, basically
	{
		LastTalk = ""
	}
}

OnBoot {
	"\1\s[10]\0\s[0]"; --
	"(It's the community lamppost.)"
	"(Still covered in posters.)"
	"(Maybe there's even a new one today...)"
	"(Some of these numbers don't even go anywhere. How do you even type \m on the numpad?)"
}

OnClose {
	"\1\s[10]\0\s[0]\![bind,poster,blank,0]\![bind,faces,%(sessionFace),0]\![bind,text,%(sessionText),0]"; --
	"(Dawn is when I sleep.)"
	"(No matter how strong my light, some days I worry I won't wake up.)"
	"(I miss my heartbeat.)"
	"(Oh, there comes someone now...)"
	--
	"\x\-"
}

OnNotifyDressupInfo : all {
	facesPool = IARRAY
	textPool = IARRAY
	rainPool = IARRAY
	dressupinfo = reference
	//dressupinfo[0] = IARRAY //rain
	//dressupinfo[0] = IARRAY //blank poster
	//if you're making a shell you have to remember these! otherwise go crazy idc
	foreach dressupinfo; _element {
		_element = SPLIT(_element,"%(C_BYTE1)")
		if _element[1] == "faces"; facesPool ,= _element[2]
		elseif _element[1] == "text"; textPool ,= _element[2]
		elseif _element[1] == "rain"; rainPool ,= _element[2]
		else; continue
	}
}